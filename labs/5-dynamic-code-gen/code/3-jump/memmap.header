/* 
 * This is an example of a linker script from cs140e that defines various
 * values.
 *
 * You should modify it (or feel free to replace it!) to do the lab.
 * Note: you will have to modify the makefile.
 */
SECTIONS
{
    LINK_ADDR = 0x8000;

    /* our simple a.out header.  will add as needed. */
    .crt0_header LINK_ADDR : {
        /* magic cookie for sanity checking */
        LONG(0xea000000 | (SIZEOF(.crt0_header) - 8) >> 2);
        BYTE(72);
        BYTE(101);
        BYTE(108);
        BYTE(108);
        BYTE(111);
        BYTE(10);
        BYTE(0);
        BYTE(0);
        LONG(0);
        /* can add stuff below and loader should still work */
    }
	.text :  { KEEP(*(.text.boot))  *(.text*) }
    .data :   { . = ALIGN(4); *(.data*)   . = ALIGN(4); } 
    .rodata : { . = ALIGN(4); *(.rodata*) . = ALIGN(4); }
	.bss : {
        __bss_start__ = .;
        *(.bss*)
        . = ALIGN(4);
        *(COMMON)
        . = ALIGN(8);
        __bss_end__ = .;
        . = ALIGN(8);
        __heap_start__ = .;
    }
}
